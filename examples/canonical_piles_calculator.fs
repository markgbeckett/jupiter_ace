2 CONSTANT CELL

: COUNT ( ADDR -- ADDR LEN )
    ( CONVERT COUNTABLE STRING TO TYPE FORMAT )
    DUP 1+ SWAP C@
;

0 VARIABLE DENSITY
0 VARIABLE THETA
0 VARIABLE ID

: " ( -- )
    ( TRANSFER QUOTE-DELIMITED STRING TO PAD )
    ASCII " WORD
;

: INSERT_STRING ( ADDR LEN -- )
    ( INSERT STRING FROM PAD INTO DICTIONARY )
    0 DO
	DUP C@ C,
	1+
    LOOP
    
    DROP
;

DEFINER MATERIAL ( STRING DENISITY THETA -- )
  ( DEFINE WORD CONTAINING MATERIAL PROPERTIES )
  , , 
  DUP C@ 1+ INSERT_STRING 
DOES>
  ( COPY PROPERTIES FROM WORD INTO RELEVANT VARIABLES )
  DUP @ THETA !
  CELL + DUP @ DENSITY !
  CELL + ID !
;

: .SUBSTANCE ( -- )
    ( PRINT MATERIAL NAME )
    ID @
    COUNT TYPE
;

: FOOT 10 * ;

: INCH 100 12 */ 5 + 10 / + ;

: /TAN 1000 THETA @ */ ;

: PILE ( N -- )
    DUP DUP 10 */ 1000 */ 355 339 */ /TAN /TAN
    DENSITY @ 200 */ ." = " . ." TONS OF " .SUBSTANCE
;

" CEMENT" 131 700 MATERIAL CEMENT
" LOOSE GRAVEL" 93 649 MATERIAL LOOSE-GRAVEL
" PACKED GRAVEL" 100 700 MATERIAL PACKED-GRAVEL
" DRY SAND" 90 754 MATERIAL DRY-SAND
" WET SAND" 118 900 MATERIAL WET-SAND
" CLAY" 120 727 MATERIAL CLAY
