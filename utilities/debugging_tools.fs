DECIMAL

: .S ( -- )
    ( PRINT CONTENTS OF DATA STACK )
    ( REPRODUCED FROM JUPITER ACE MANUAL, P. 143 )
    15419 @ HERE 12 + ( TOP, BOTTOM )
    OVER OVER -
    IF ( STACK IS NOT EMPTY )
	DO
	    I @ .
	2 +LOOP
    ELSE
	DROP DROP
    THEN
;

: .BYTE ( N -- )
    ( PRINT TWO-DIGIT BYTE )
    ( ASSUMES BASE 16 )
    00 ( CONVERT TO DOUBLE )
    <# # # #> ( CREATE FORMATTED VERSION )
    TYPE ( OUTPUT IT )
;

: .WORD ( N -- )
    ( PRINT FOUR-DIGIT WORD )
    ( ASSUMES BASE 16 )
    00 ( CONVERT TO DOUBLE )
    <# # # # # #> ( CREATE FORMATTED VERSION )
    TYPE ( OUTPUT IT )
;

: .CHAR8 ( ADDR -- )
    ( DUMP EIGHT BYTES OF MEMORY AS ASCII )
    ( NON-PRINTABLE CHARACTERS REPLACED BY '.' )
    8 0 DO
	DUP C@

	( CHECK IF PRINTABLE )
	DUP 127 AND 16 < IF
	    DROP ASCII .
	ELSE
	    DUP 127 AND DUP
	    23 > SWAP 32 < AND IF
		DROP ASCII .
	    THEN
	THEN

	EMIT

	1+
    LOOP

    DROP ( DISCARD ADDRESS )
;

: DUMP ( ADDR N -- )
    ( DUMP SECTION OF MEMORY TO SCREEN )
    BASE C@ >R ( SAVE CURRENT BASE )
    16 BASE C! ( SWITCH TO HEXADECIMAL )

    ( DIVIDE NO BYTES TO PRINT BY 8 AND ROUND UP )
    8 /MOD SWAP IF 1+ THEN 0 DO
	CR

	DUP .WORD ( PRINT ADDRESS )

	SPACE

	DUP 8 0 DO ( PRINT HEX DIGITS )
	    DUP C@ .BYTE
	    1+
	LOOP DROP
	
	SPACE

	DUP .CHAR8 ( PRINT ASCII VALUES )

	8 + ( ADVANCE ADDRESS BY 8 )
    LOOP

    CR

    DROP ( DISCARD ADDRESS )

    R> BASE C! ( RESTORE BASE )
;
