DECIMAL 16 BASE C!

CREATE MCODE 600 ALLOT

: GETADDR ( N -- ADDR )
    ( RETRIEVE ADDR OF CHANNEL INFO  )
    2 * MCODE + @
    1+
;

: COUNT ( PAD -- PAD+1 LEN )
    ( DEREFERENCE STRING READ WITH 'WORD' )
    DUP 1+ SWAP C@
;

DEFINER PLAY ( S:NOTES -- )
    ( DEFINE NEW MUSIC WORD )
    0 ( START WITH CHANNEL 0 )
    BEGIN
	20 WORD COUNT ( FIND AND DEREFERENCE NEXT STRING )
	DUP C, ( STORE LENGTH IN PARAM FIELD )
	OVER + SWAP ( WORK OUT LIMITS FOR LOOP )
	DO
	    I C@ C, ( RETRIEVE EACH CHARACTER AND ADD TO PARAM FIELD )
	LOOP
	1+ ( NEXT CHANNEL )
	DUP 3 = ( CHECK IF DONE )
    UNTIL
    DROP ( BALANCE STACK )
DOES>
    0 ( START WITH CHANNEL 0 )
    BEGIN
	>R ( STORE CHANNEL NUMBER )
	DUP 1+ ( FIND START OF STRING )
	SWAP C@ ( RETRIEVE LENGTH OF STRING )
	OVER OVER + ( CALCULATE END OF STRING )
	>R ( STORE END OF STRING )
	OVER + ( CALCULATE END OF STRING )
	I' GETADDR 4 + ( CALC POINTER INTO CHANNEL INFO )
	! ( SAVE END OF STRING )
	I' GETADDR ( CALC POINTER INTO CHANNEL INFO )
	! ( SAVE START OF STRING )
	R> ( RETRIEVE END OF STRING )
	R> ( RETRIEVE CHANNEL NUMBER )
	1+ ( NEXT CHANNEL )
	DUP 3 = ( CHECK IF DONE )
    UNTIL
    DROP DROP ( BALANCE STACK )
    MCODE 6 + CALL ( CALL PLAY ROUTINE )
;

: GETNUM ( -- NUM )
  INVIS ( DISABLE INPUT ECHOING )
  BEGIN
    QUERY NUMBER ( TRY TO READ NUMBER )
    DUP 1055 = IF ( FLOATING-POINT IDENTIFIED )
      DROP DROP DROP ( CLEAR STACK, READY TO TRY AGAIN )
      0
    ELSE
      1006 = ( INTEGER IDENTIFIED? )
    THEN
  UNTIL
  VIS ( REENABLE INPUT ECHOING )
;

: SCONFIG ( -- )
    ( DISPLAY CURRENT VALUES )
    CR ." CURRENT VALUES" CR
    
    ." REGPORT = "
    400D @ U.
    CR

    ." READPORT = "
    4013 @ U.
    CR
    
    ." WRITEPORT = "
    401F @ U.
    CR
    
    CR ." NEW VALUES" CR
    
    ." REGPORT = "
    GETNUM DUP U. CR
    DUP 400D ! 401A !

    ." READPORT = "
    GETNUM DUP U. CR
    401F !

    ." WRITEPORT = "
    GETNUM DUP U. CR
    4013 !
;

DECIMAL

PLAY HOTMK O5T120N3e#fgabg5bN3#a#f5#a3af5aN3e#fgabgbENDbgb7D O5V6N3b#C#DE#F#D5#FN3G#D5G3#F#D5#FN3b#C#DE#F#D5#FN3G#D5G7#F O5V6N3Dbgb7DN5&E7&N5&E7&N3e#fgabgbE
